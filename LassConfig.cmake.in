set(Lass_VERSION @Lass_VERSION@)
set(Lass_MAJOR_VERSION @Lass_VERSION_MAJOR@)
set(Lass_MINOR_VERSION @Lass_VERSION_MINOR@)
set(Lass_PATCHLEVEL @Lass_VERSION_PATCH@)

set(Lass_HAVE_PYTHON "@Python_Development_FOUND@")
if(Lass_HAVE_PYTHON)

    set(Lass_PYTHON_VERSION "@Python_VERSION_MAJOR@.@Python_VERSION_MINOR@")
    set(Lass_PYTHON_DEBUG_POSTFIX "@LASS_PYTHON_DEBUG_POSTFIX@")

    list(APPEND CMAKE_MODULE_PATH "${CMAKE_CURRENT_LIST_DIR}")

    set(_Lass_FIND_PYTHON_ARGS Python "${Lass_PYTHON_VERSION}" EXACT
                               COMPONENTS Interpreter Development)

    include(CMakeFindDependencyMacro)
    find_dependency(${_Lass_FIND_PYTHON_ARGS})

endif()


if(EXISTS "${CMAKE_CURRENT_LIST_DIR}/LassBuildExports.cmake")
    # we're in the build tree, use the build tree exports
    include("${CMAKE_CURRENT_LIST_DIR}/LassBuildExports.cmake")

    get_target_property(_lass_INCLUDE_DIRS Lass::lass INTERFACE_INCLUDE_DIRECTORIES)
    list(GET _lass_INCLUDE_DIRS 0 _lass_ROOT_DIR)
    include("${_lass_ROOT_DIR}/tools/LassStubgen.cmake")
    unset(_lass_INCLUDE_DIRS)
    unset(_lass_ROOT_DIR)
else()
    include("${CMAKE_CURRENT_LIST_DIR}/LassExports.cmake")
    include("${CMAKE_CURRENT_LIST_DIR}/LassStubgen.cmake")
endif()


set(Lass_LIBRARIES Lass::lass)
get_target_property(Lass_INCLUDE_DIRS Lass::lass INTERFACE_INCLUDE_DIRECTORIES)

if(Lass_HAVE_PYTHON)
    list(APPEND Lass_LIBRARIES Lass::lass_python)
    list(APPEND Lass_INCLUDE_DIRS ${Python_INCLUDE_DIRS})
endif()


# -- deprecated variables ---

function(__lass_deprecated_vars var access)
    if (access STREQUAL READ_ACCESS)
        message(DEPRECATION "The variable '${var}' is deprecated.")
    endif()
endfunction()


set(Lass_PYTHON_LIBRARY "${Python_LIBRARY_RELEASE}")
variable_watch(Lass_PYTHON_LIBRARY __lass_deprecated_vars)

set(Lass_PYTHON_DEBUG_LIBRARY "${Python_LIBRARY_DEBUG}")
variable_watch(Lass_PYTHON_DEBUG_LIBRARY __lass_deprecated_vars)

set(Lass_PYTHON_EXECUTABLE "${Python_EXECUTABLE}")
variable_watch(Lass_PYTHON_EXECUTABLE __lass_deprecated_vars)

set(Lass_BUILD_WITHOUT_ITERATOR_DEBUGGING @BUILD_WITHOUT_ITERATOR_DEBUGGING@)
variable_watch(Lass_BUILD_WITHOUT_ITERATOR_DEBUGGING __lass_deprecated_vars)

set(Lass_EXTRA_CXX_FLAGS "$<$<BOOL:$<TARGET_PROPERTY:Lass::lass,INTERFACE_COMPILE_OPTIONS>>:$<JOIN:$<TARGET_PROPERTY:Lass::lass,INTERFACE_COMPILE_OPTIONS>, >>")
variable_watch(Lass_EXTRA_CXX_FLAGS __lass_deprecated_vars)
